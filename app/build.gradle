plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId "com.google.android.stardroid"
        minSdkVersion 26
        targetSdkVersion 31
        versionCode 1541
        versionName "1.10.1 - RC1"
        buildConfigField 'String', 'GOOGLE_ANALYTICS_CODE', '""'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            if (project.file('no-checkin.properties').exists()) {
                Properties properties = new Properties()
                // Sky Map devs should create this file with the signing passwords
                properties.load(project.file('no-checkin.properties').newDataInputStream())
                storeFile file("stardroid-release-key.keystore")
                storePassword properties.getProperty('store-pwd')
                keyPassword properties.getProperty('key-pwd')
                keyAlias "stardroid"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
            if (file('no-checkin.properties').exists()) {
                signingConfig signingConfigs.release
                Properties properties = new Properties()
                // Sky Map devs should create this file with the Google Analytics key
                properties.load(project.file('no-checkin.properties').newDataInputStream())
                def analyticsKey = properties.getProperty('analytics-key')
                buildConfigField 'String', 'GOOGLE_ANALYTICS_CODE', analyticsKey
            }
        }
    }


    buildFeatures {
        viewBinding true
    }

    // This enables long timeouts required on slow environments, e.g. Travis

    testOptions {
        animationsDisabled = true
    }


    flavorDimensions "default"
    productFlavors {
        // includes gms Google services
        gms {
            resValue "string", "analytics_enabled", "true"
        }

        // uses only open source software
        fdroid {
            resValue "string", "analytics_enabled", "false"
        }
    }

    installation {
        installOptions '-d', '-t'
    //    timeOutInMs '10 * 60 * 1000'
    }
    lint {
        abortOnError false
    }
}

dependencies {
    implementation project(path: ':datamodel')

//    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

// Android support
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.preference:preference:1.2.0'
    implementation "androidx.core:core-ktx:1.7.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'com.google.android.material:material:1.5.0'

// Third-party
    implementation 'com.google.guava:guava:30.1.1-jre'
    def dagger_version = "2.40"
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    // For annotations not in Android but needed by Dagger
    implementation 'javax.annotation:jsr250-api:1.0'
    // Lite version for Android - it's lighter (obviously) but also doesn't use reflection
    // which can cause no end of problems with minifiers like proguard.
    implementation 'com.google.protobuf:protobuf-javalite:3.13.0'

// Flavor
    gmsImplementation 'com.google.android.gms:play-services-analytics:18.0.0'
    gmsImplementation 'com.google.android.gms:play-services-location:19.0.0'
    gmsImplementation 'com.google.firebase:firebase-analytics:20.0.2'

// Testing
    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    testImplementation 'org.robolectric:robolectric:4.4'
    testImplementation 'org.easymock:easymock:2.5.2'
    testImplementation 'com.google.truth:truth:1.0.1'
    androidTestImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'org.easymock:easymock:2.5.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    // Optional -- Hamcrest library
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    // Optional -- UI testing with Espresso
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    // Optional -- UI testing with UI Automator
    // androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
}
